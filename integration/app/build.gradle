plugins {
    id 'application'
    id 'com.google.protobuf' version '0.9.5'
    id 'com.gradleup.shadow' version '8.3.6'
}

repositories {
    mavenCentral()
    mavenLocal()
}

application {
    mainClass = ''
}

dependencies {
    testImplementation libs.junit.jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'

    implementation project(":lib")

    if (JavaVersion.current().isJava9Compatible()) {
        // Workaround for @javax.annotation.Generated
        // see: https://github.com/grpc/grpc-java/issues/3633
        implementation 'javax.annotation:javax.annotation-api:1.3.1'
    }

    implementation 'commons-cli:commons-cli:1.9.0'
    implementation 'ch.qos.logback:logback-classic:1.5.18'
    implementation 'com.google.protobuf:protobuf-java:4.30.2'
    implementation 'io.grpc:grpc-protobuf:1.72.0'
    implementation 'io.grpc:grpc-stub:1.72.0'
    implementation 'io.grpc:grpc-netty-shaded:1.72.0'
    implementation 'io.projectreactor.netty:reactor-netty-http:1.2.5'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.3'

    protobuf files("proto/")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:4.30.2'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.72.0'
        }
    }
    generateProtoTasks {
        ofSourceSet('main').configureEach {
            plugins {
                grpc {}
            }
        }
    }
}

shadowJar {
    mergeServiceFiles()
}

def dockerfilePath = file("Dockerfile").absolutePath
def dockerContext = rootProject.projectDir.absolutePath

tasks.register('buildClientDockerImage', Exec) {
    group = 'docker'
    description = 'Builds Docker image for client target in integration app'
    workingDir = dockerContext
    commandLine 'sh', '-c', "docker build -f ${dockerfilePath} -t client --target client ${dockerContext}"
}

tasks.register('buildServerDockerImage', Exec) {
    group = 'docker'
    description = 'Builds Docker image for server target in integration app'
    workingDir = dockerContext
    commandLine 'sh', '-c', "docker build -f ${dockerfilePath} -t server --target server ${dockerContext}"
}

tasks.register('buildDockerImages') {
    group = 'docker'
    description = 'Builds both client and server Docker images'
    dependsOn 'buildClientDockerImage', 'buildServerDockerImage'
}
